buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
        classpath 'org.kordamp.gradle:jdeps-gradle-plugin:0.2.0'
    }
}

subprojects {
    afterEvaluate {
        repositories {
            mavenCentral()
        }

        jar {
            inputs.property("moduleName", moduleName)
            manifest {
                attributes('Automatic-Module-Name': moduleName)
            }
        }

        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'org.junit.jupiter.api',
                        '--add-reads', "$moduleName=org.junit.jupiter.api",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }
    }
}

ext {
    undertowVersion = '1.4.21.Final'
    guavaVersion = '23.6-jre'
    apiguardianVersion = '1.0.0'
    jacksonVersion = '2.9.3'
    slf4jVersion = '1.7.25'
    log4j2Version = '2.10.0'
    hcVersion = '4.5.4'
    // Test library versions
    junitVersion = '5.0.2'
    assertjVersion = '3.8.0'
    mockitoVersion = '2.13.0'

    libraries = [
            apiguardian: "org.apiguardian:apiguardian-api:$apiguardianVersion",
            undertow   : ["io.undertow:undertow-core:$undertowVersion"],
            guava      : "com.google.guava:guava:$guavaVersion",
            jackson    : ["com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"],
            httpclient : "org.apache.httpcomponents:httpclient:$hcVersion",
            log4j_api  : "org.apache.logging.log4j:log4j-api:$log4j2Version",
            log4j_impl : "org.apache.logging.log4j:log4j-core:$log4j2Version",

            junit5     : ["org.junit.jupiter:junit-jupiter-api:$junitVersion",
                          "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
                          "org.junit.jupiter:junit-jupiter-params:$junitVersion"],
            assertj    : "org.assertj:assertj-core:$assertjVersion",
            mockito    : ["org.mockito:mockito-core:$mockitoVersion"]
    ]
}




